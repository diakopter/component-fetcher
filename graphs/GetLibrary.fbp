EXPORT=REPO.IN:REPOSITORY
EXPORT=GETPACKAGE.TOKEN:TOKEN
EXPORT=CLEARGROUPS.OUT:COMPONENTS
EXPORT=SENDPACKAGE.OUT:PACKAGE

'package.json' -> STRING PackagePath(SendString)
PackagePath() OUT -> PATH GetPackage(github/GetContents)

Repo(Throttle) OUT -> IN SplitRepo(Split)
GetPackage() LOAD -> LOAD Repo()

SplitRepo(Split) OUT -> REPOSITORY GetPackage()
SplitRepo() OUT -> IN PackagePath()
GetPackage() OUT -> IN RemoveFilename(RemoveGroups)
GetPackage() ERROR -> IN DisplayError(Output)
RemoveFilename() OUT -> IN ParsePackage(ParseJson)
'name' -> KEY GroupByLibrary(GroupByObjectKey)
ParsePackage() OUT -> IN SplitPackage(Split)
SplitPackage() OUT -> IN GroupByLibrary()
SplitPackage() OUT -> IN SendPackage(Repeat)
'noflo' -> KEY GetNoFlo(GetObjectKey)
GroupByLibrary() OUT -> IN GetNoFlo()
GetNoFlo() OUT -> IN SplitNoFlo(Split)
GetNoFlo() MISSED -> IN DropMissedLibraries(Drop)
'components' -> KEY GetComponents(GetObjectKey)
SplitNoFlo() OUT -> IN GetComponents()
GetComponents() OUT -> IN MergeComponents(Merge)
GetComponents() MISSED -> IN DropMissedComponents(Drop)
#'graphs' -> KEY GetGraphs(GetObjectKey)
#SplitNoFlo() OUT -> IN GetGraphs()
#GetGraphs() OUT -> IN MergeComponents(Merge)
#GetGraphs() MISSED -> IN DropMissedGraphs(Drop)
MergeComponents() OUT -> IN ClearGroups(FirstGroup)
