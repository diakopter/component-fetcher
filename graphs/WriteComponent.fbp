EXPORT=SPLITCOMPONENT.IN:COMPONENT
EXPORT=DESTINATION.IN:DESTINATION
EXPORT=GENERATED.OUT:GENERATED
EXPORT=ERRORS.OUT:ERRORS

# Build filename
'/' -> DELIMITER FullFilename(CompileString)
Destination(Split) OUT -> STRING PostPath(SendString)
SplitComponent(Split) OUT -> IN PostPath() OUT -> IN FullFilename()
SplitComponent() OUT -> IN PostName(componentfetcher/BuildFilename)
PostName() OUT -> IN FullFilename()

# Transform post
'title' -> ACCEPT FilterMetadata(FilterProperty)
'library' -> ACCEPT FilterMetadata()
'layout=component' -> PROPERTY SetLayout(SetProperty)
SplitComponent() OUT -> IN FilterMetadata()
FilterMetadata() OUT -> IN SetLayout() OUT -> IN GetMetadata(yaml/ToYaml)

# Merge metadata and contents to a file
GetMetadata() OUT -> IN SplitMetadata(Split) OUT -> IN BuildContents(Concat)
'---' -> STRING Separator(SendString)
SplitComponent() OUT -> IN Separator() OUT -> IN BuildContents()
'title' -> KEY GetName(GetObjectKey)
SplitComponent() OUT -> IN GetName()
GetName() OUT -> IN BuildContents()
BuildContents() OUT -> IN CombineContents(CompileString)

FullFilename() OUT -> STRING SendFilename(SendString)
SendFilename() OUT -> FILENAME Save(filesystem/WriteFile)
CombineContents() OUT -> IN SendContents(Split)
SendContents() OUT -> IN Save()
SendContents() OUT -> IN SendFilename()

Save() OUT -> IN Generated(Merge)
Save() ERROR -> IN Errors(Merge)
