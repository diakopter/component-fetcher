EXPORT=SPLITCOMPONENT.IN:COMPONENT
EXPORT=DESTINATION.IN:DESTINATION
EXPORT=GENERATED.OUT:GENERATED
EXPORT=ERRORS.OUT:ERRORS

# Build filename
'/' -> DELIMITER FullFilename(CompileString)
Destination(Split) OUT -> STRING PostPath(SendString)
SplitComponent(Split) OUT -> IN PostPath() OUT -> IN FullFilename()
'title' -> KEY GetComponentName(GetObjectKey)
SplitComponent() OUT -> IN GetComponentName()
'library' -> KEY GetLibraryName(GetObjectKey)
SplitComponent() OUT -> IN GetLibraryName()
'2013-07-12-' -> STRING SendDate(SendString)
'' -> DELIMITER CompileName(CompileString)
SendDate() OUT -> IN CompileName()
GetComponentName() OUT -> IN SplitComponentName(Split)
SplitComponentName() OUT -> IN SendDate()
GetLibraryName() OUT -> STRING SendLibrary(SendString)
SendLibrary() OUT -> IN CompileName()
SplitComponentName() OUT -> IN SendLibrary()
SplitComponentName() OUT -> IN CompileName()
'.md' -> STRING SendSuffix(SendString)
SplitComponentName() OUT -> IN SendSuffix()
SendSuffix() OUT -> IN CompileName()
CompileName() OUT -> IN FullFilename()

# Transform post
'title' -> ACCEPT FilterMetadata(FilterProperty)
'library' -> ACCEPT FilterMetadata()
'layout=library' -> PROPERTY SetLayout(SetProperty)
SplitComponent() OUT -> IN FilterMetadata()
FilterMetadata() OUT -> IN SetLayout() OUT -> IN GetMetadata(yaml/ToYaml)

# Merge metadata and contents to a file
GetMetadata() OUT -> IN SplitMetadata(Split) OUT -> IN BuildContents(Concat)
'---' -> STRING Separator(SendString)
SplitComponent() OUT -> IN Separator() OUT -> IN BuildContents()
'' -> DATA KickContents(Kick) OUT -> IN BuildContents()
SplitMetadata() OUT -> IN KickContents()
BuildContents() OUT -> IN CombineContents(CompileString)

FullFilename() OUT -> FILENAME Save(filesystem/WriteFile)
CombineContents() OUT -> IN Save()
Save() OUT -> IN Generated(Merge)
Save() ERROR -> IN Errors(Merge)
